apply plugin: 'com.android.application'
// TaurusXCenter
apply plugin: 'com.taurusx.center'

// flavor 多 dimension
// 1、建议仅在一个 dimension 中 配置
// 2、如果在多个 dimendion 中配置，则按照 flavorDimensions 中的次序来查找配置
// 3、flavor 中不配置或配置 null 都表示不接入

android {
    compileSdkVersion 29

    signingConfigs {
        defaultConfig {
            applicationId "com.taurusx.ads.demo"
            minSdkVersion 19
            targetSdkVersion 28
            versionCode 1
            versionName "1.0"
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            multiDexEnabled = true

            ndk { abiFilters 'armeabi-v7a' }
        }
        config {
            storeFile file('../debug.jks')
            storePassword 'android'
            keyAlias 'debug'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }



    // 下面的示例中，在两个 dimension 中都有配置，则优先使用 "channel" 中的配置
    // 只有当 "channel" 中无配置时，才使用 "price" 中的配置
    flavorDimensions "channel", "price"

    // 在 productFlavors 中：
    // "channel": toutiao_inner(有配置)，nullTaurusX(有配置 null)，noTaurusX(无配置)
    // "price":   paid(有配置)，         free(无配置)

    // 则配置结果如下：
    // toutiao_innerPaid：使用 toutiao_inner 中的配置
    // toutiao_innerFree：使用 toutiao_inner 中的配置

    // nullTaurusXPaid：使用 nullTaurusX 中的配置（null 表示不接入）
    // nullTaurusXFree：使用 nullTaurusX 中的配置（null 表示不接入）

    // noTaurusXPaid：使用 paid 中的配置
    // noTaurusXFree：无配置（不接入）
    productFlavors {
        toutiao_inner {
            dimension "channel"
            taurusXCenter {
                dependencies {
                    taurusXAds true
                    taurusXAdsNetwork = [
                            toutiao : true
                    ]
                }
            }
        }

        nullTaurusX {
            dimension "channel"
            taurusXCenter {
                dependencies null
            }
        }

        noTaurusX {
            dimension "channel"
        }

        paid {
            dimension "price"
            taurusXCenter {
                dependencies {
                    taurusXAds true
                    taurusXAdsNetwork = [
                            kuaishou : true
                    ]
                }
            }
        }

        free {
            dimension "price"
        }
    }
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
}

configurations.all {
    resolutionStrategy {
        force 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
        force 'androidx.lifecycle:lifecycle-viewmodel:2.1.0'
        force 'androidx.appcompat:appcompat:1.2.0-alpha02'
        force 'androidx.fragment:fragment:1.1.0'
        force 'androidx.lifecycle:lifecycle-runtime:2.1.0'
        force 'androidx.arch.core:core-common:2.1.0'
        force 'androidx.lifecycle:lifecycle-common:2.1.0'
        force 'com.google.code.gson:gson:2.8.5'
    }
}